plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

loom {
    runs {
        create("data") {
            client()
            name("Data Generation")
            vmArg("-Dfabric-api.datagen")
            vmArg "-Dfabric-api.datagen.output-dir=${project(':common').file("src/main/generated")}"
            vmArg("-Dfabric-api.datagen.modid=witchery")

            runDir("build/datagen")
        }
    }
}

repositories {
    maven {
        url = 'https://jitpack.io/'
    }
    maven{
        url = "https://api.modrinth.com/maven"
    }
    maven {
        url = 'https://maven.ladysnake.org/releases'
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"

    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    modImplementation("net.fabricmc:fabric-language-kotlin:${rootProject.fabric_language_kotlin_version}")

    modImplementation "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    include(modImplementation 'com.github.Chocohead:Fabric-ASM:v2.3')

    modCompileOnly "dev.emi:emi-fabric:${emi_version}:api"
    modLocalRuntime "dev.emi:emi-fabric:${emi_version}"

    //modLocalRuntime "maven.modrinth:sodium:mc1.21-0.6.0-beta.2-fabric"

    include(modImplementation "io.github.ladysnake:PlayerAbilityLib:${pal_version}")

    //Stupid modonomicon
    runtimeOnly "org.commonmark:commonmark:${commonmark_version}"
    runtimeOnly "org.commonmark:commonmark-ext-gfm-strikethrough:${commonmark_version}"
    runtimeOnly "org.commonmark:commonmark-ext-ins:${commonmark_version}"
    runtimeOnly "me.zeroeightsix:fiber:${fiber_version}"

    modImplementation "dev.emi:trinkets:${trinkets_version}"

    modImplementation ("com.klikli_dev:modonomicon-${minecraft_version}-fabric:${modonomicon_version}"){
        transitive = false
    }

    modImplementation("com.terraformersmc:modmenu:11.0.2")

    modCompileOnly "mcp.mobius.waila:wthit-api:fabric-${wthitVersion}"

    modRuntimeOnly "mcp.mobius.waila:wthit:fabric-${wthitVersion}"
    modRuntimeOnly "lol.bai:badpackets:fabric-${badpacketsVersion}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
