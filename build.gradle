plugins {
    id 'dev.architectury.loom' version '1.9.428' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.gradleup.shadow' version '8.3.6' apply false
    id "org.jetbrains.kotlin.jvm" version "2.0.21" apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'
    apply plugin: "org.jetbrains.kotlin.jvm"

    base {
        archivesName = "$rootProject.mod_id-$project.name"
    }

    repositories {
        maven {
            url = 'https://thedarkcolour.github.io/KotlinForForge/'
        }
        maven {
            url = "https://maven.parchmentmc.org"
        }
        maven {
            url = "https://maven.terraformersmc.com/"
        }
        maven {
            url = "https://dl.cloudsmith.io/public/klikli-dev/mods/maven/"
        }

        maven { url 'https://maven.fabricmc.net' }
        maven { url 'https://maven.shedaniel.me/' }

        maven {
            url "https://maven2.bai.lol"
        }
        maven {
            name = 'Ladysnake Mods'
            url = 'https://maven.ladysnake.org/releases'
        }
    }

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-1.21.1:2024.11.17@zip")
        }
    }

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        repositories {
        }
    }
}
